#!/bin/zsh
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
[[ -a ~/.localrc ]] && source ~/.localrc

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

antigen bundle rupa/z
antigen bundle Tarrasch/zsh-bd
antigen bundle djui/alias-tips
antigen bundle caarlos0/zsh-mkc
antigen bundle sindresorhus/pure
antigen bundle oknowton/zsh-dwim
antigen bundle Tarrasch/zsh-colors
antigen bundle caarlos0/zsh-git-sync
antigen bundle zsh-users/zsh-completions
antigen bundle caarlos0/zsh-open-github-pr
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle zsh-users/zsh-history-substring-search

antigen apply

